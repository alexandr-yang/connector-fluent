
#Область ОписаниеПеременных

// Контекст конструктора
Перем _Контекст;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Инициализировать конструктор HTTP-запроса
//
// Параметры:
//  Сессия - Структура - см. КоннекторHTTP.СоздатьСессию()
//         - Неопределено
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция Инициализировать(Сессия = Неопределено) Экспорт
	
	_Контекст = НовыйКонтекст();
	
	Если ТипЗнч(Сессия) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(_Контекст.Сессия, Сессия);
	КонецЕсли;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить метод HTTP-запроса
//
// Параметры:
//  Метод - Строка
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция Метод(Метод) Экспорт
	
	_Контекст.Метод = Метод;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить адрес сервера
//
// Параметры:
//  АдресСервера - Строка
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция Сервер(АдресСервера) Экспорт
	
	_Контекст.Сервер = ПодготовитьАдресСервера(АдресСервера);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить порт
//
// Параметры:
//  Порт - Число
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция Порт(Порт) Экспорт
	
	_Контекст.Порт = Порт;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить адрес ресурса
//
// Параметры:
//  АдресРесурса - Строка
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция АдресРесурса(АдресРесурса) Экспорт
	
	_Контекст.АдресРесурса = ПодготовитьАдресРесурса(АдресРесурса);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить тело HTTP-запроса
//
// Параметры:
//  ТелоЗапроса - Структура, Массив, Соответствие
//              - ДвоичныеДанные, Строка
//  ЗаписатьJSON - Булево
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция ТелоЗапроса(ТелоЗапроса, ЗаписатьJSON = Ложь) Экспорт
	
	Если ЗаписатьJSON Тогда
		_Контекст.ДополнительныеПараметры.JSON = ТелоЗапроса;
	Иначе
		_Контекст.ДополнительныеПараметры.Данные = ТелоЗапроса;
	КонецЕсли;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Добавить файл для отправки
//
// Параметры:
//  Имя - Строка - Имя поля формы
//  ИмяФайла - Строка
//  Данные - ДвоичныеДанные - Двоичные данные файла
//  Тип - Строка - MIME-тип файла
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция ДобавитьФайл(Имя, ИмяФайла, Данные = Неопределено, Тип = Неопределено) Экспорт

	Файл = КоннекторHTTP.НовыйОтправляемыйФайл(Имя, ИмяФайла, Данные, Тип); // BSLLS:MissingCommonModuleMethod-off

	_Контекст.ДополнительныеПараметры.Файлы.Добавить(Файл);
	
	Возврат ЭтотОбъект;

КонецФункции

// Добавить заголовок HTTP-запроса
//
// Параметры:
//  Ключ - Строка
//  Значение - Строка
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция ДобавитьЗаголовок(Ключ, Значение) Экспорт
	
	_Контекст.ДополнительныеПараметры.Заголовки.Вставить(Ключ, Значение);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Добавить Cookies HTTP-запроса
//
// Параметры:
//  Ключ - Строка
//  Значение - Строка
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPСессии
//
Функция ДобавитьКуки(Ключ, Значение) Экспорт

	_Контекст.ДополнительныеПараметры.Cookies.Вставить(Ключ, Значение);
	
	Возврат ЭтотОбъект;

КонецФункции

// Добавить параметры HTTP-запроса
//
// Параметры:
//  Ключ - Строка
//  Значение - Строка
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция ДобавитьПараметрЗапроса(Ключ, Значение) Экспорт
	
	Если _Контекст.ДополнительныеПараметры.ПараметрыЗапроса = Неопределено Тогда
		_Контекст.ДополнительныеПараметры.ПараметрыЗапроса = Новый Структура;
	КонецЕсли;
	
	_Контекст.ДополнительныеПараметры.ПараметрыЗапроса.Вставить(Ключ, Значение);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить таймаут выполнения HTTP-запроса
//
// Параметры:
//  Таймаут - Число
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция Таймаут(Таймаут) Экспорт
	
	_Контекст.ДополнительныеПараметры.Таймаут = Таймаут;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Использовать защищенное соединение HTTPS
//
// Параметры:
//  Значение - Булево
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция ЗащищенноеСоединение(Значение = Истина) Экспорт
	
	_Контекст.ЗащищенноеСоединение = Значение;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Проверять сертификат сервера
//
// Параметры:
//  Значение - Булево, СертификатыУдостоверяющихЦентровФайл
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPСессии
//
Функция ПроверятьSSL(Значение) Экспорт

	_Контекст.ДополнительныеПараметры.ПроверятьSSL = Значение;

	Возврат ЭтотОбъект;

КонецФункции

// Использовать клиентский сертификатор SSL
//
// Параметры:
//  Сертификат - СертификатКлиентаФайл, СертификатКлиентаWindows
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPСессии
//
Функция КлиентскийСертификатSSL(Сертификат) Экспорт

	_Контекст.ДополнительныеПараметры.КлиентскийСертификатSSL = Сертификат;

	Возврат ЭтотОбъект;

КонецФункции

// Использовать прокси для выполнения HTTP-запроса
//
// Параметры:
//  ИнтернетПрокси - ИнтернетПрокси
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция Прокси(ИнтернетПрокси) Экспорт

	_Контекст.Сессия.Прокси = ИнтернетПрокси;

	Возврат ЭтотОбъект;

КонецФункции

// Разрешить перенаправление HTTP-запроса
//
// Параметры:
//  Значение - Булево
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция РазрешитьПеренаправление(Значение) Экспорт

	_Контекст.ДополнительныеПараметры.РазрешитьПеренаправление = Значение;

	Возврат ЭтотОбъект;

КонецФункции

// Установить максимальное время повторов между HTTP-запросами
//
// Параметры:
//  Время - Число
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция МаксимальноеВремяПовторов(Время) Экспорт
	
	_Контекст.ДополнительныеПараметры.МаксимальноеВремяПовторов = Время;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить максимальное количество повторов HTTP-запросов
//
// Параметры:
//  Количество - Число
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция МаксимальноеКоличествоПовторов(Количество) Экспорт
	
	_Контекст.ДополнительныеПараметры.МаксимальноеКоличествоПовторов = Количество;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить коэффициент экспоненциальной задержки при повторе HTTP-запроса
//
// Параметры:
//  Коэффициент - Число
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция КоэффициентЭкспоненциальнойЗадержки(Коэффициент) Экспорт

	_Контекст.ДополнительныеПараметры.КоэффициентЭкспоненциальнойЗадержки = Коэффициент;
	
	Возврат ЭтотОбъект;

КонецФункции

// Использовать Basic-аутентификацию для выполнения HTTP-запроса
//
// Параметры:
//  Пользователь - Строка
//  Пароль - Строка
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция АутентификацияBasic(Пользователь, Пароль) Экспорт
	
	Аутентификация = Новый Структура;
	Аутентификация.Вставить("Тип", "Basic");
	Аутентификация.Вставить("Пользователь", Пользователь);
	Аутентификация.Вставить("Пароль", Пароль);
	
	_Контекст.ДополнительныеПараметры.Аутентификация = Аутентификация;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Использовать Digest-аутентификацию для выполнения HTTP-запроса
//
// Параметры:
//  Пользователь - Строка
//  Пароль - Строка
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция АутентификацияDigest(Пользователь, Пароль) Экспорт

	Аутентификация = Новый Структура;
	Аутентификация.Вставить("Тип", "Digest");
	Аутентификация.Вставить("Пользователь", Пользователь);
	Аутентификация.Вставить("Пароль", Пароль);

	_Контекст.ДополнительныеПараметры.Аутентификация = Аутентификация;

	Возврат ЭтотОбъект;

КонецФункции

// Использовать AWS4-аутентификацию для выполнения HTTP-запроса
//
// Параметры:
//  ИдентификаторКлючаДоступа - Строка
//  СекретныйКлюч - Строка
//  Сервис - Строка
//  Регион - Строка
//
// Возвращаемое значение:
//  ОбработкаОбъект.КонструкторHTTPЗапроса
//
Функция АутентификацияAWS4(ИдентификаторКлючаДоступа, СекретныйКлюч, Сервис, Регион) Экспорт

	Аутентификация = Новый Структура;
	Аутентификация.Вставить("Тип", "AWS4-HMAC-SHA256");
	Аутентификация.Вставить("ИдентификаторКлючаДоступа", ИдентификаторКлючаДоступа);
	Аутентификация.Вставить("СекретныйКлюч", СекретныйКлюч);
	Аутентификация.Вставить("Сервис", Сервис);
	Аутентификация.Вставить("Регион", Регион);

	_Контекст.ДополнительныеПараметры.Аутентификация = Аутентификация;

	Возврат ЭтотОбъект;

КонецФункции

// Отправить HTTP-запрос
// 
// Возвращаемое значение:
//  ОбработкаОбъект.ОбработчикHTTPОтвета
//
Функция Отправить() Экспорт
	
	УРЛ = СформироватьУРЛ();
	Ответ = КоннекторHTTP.ВызватьМетод( // BSLLS:MissingCommonModuleMethod-off
		_Контекст.Метод,
		УРЛ,
		_Контекст.ДополнительныеПараметры,
		_Контекст.Сессия
	);
	
	Возврат КонструкторHTTPПолныеПрава.ОбработчикHTTPОтвета()
		.Инициализировать(Ответ);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Конструктор нового контекст
//
// Возвращаемое значение:
//  Структура
//    * Метод - Строка
//    * ЗащищенноеСоединение - Булево
//    * Сервер - Строка
//    * Порт - Число, Неопределено
//    * АдресРесурса - Строка
//    * ДополнительныеПараметры - Структура - см. КоннекторHTTP.НовыеПараметры()
//    * Сессия - Структура - КоннекторHTTP.СоздатьСессию()
//
Функция НовыйКонтекст()
	
	Контекст = Новый Структура;
	Контекст.Вставить("Метод", "");
	Контекст.Вставить("ЗащищенноеСоединение", Истина);
	Контекст.Вставить("Сервер", "");
	Контекст.Вставить("Порт");
	Контекст.Вставить("АдресРесурса", "");
	Контекст.Вставить("ДополнительныеПараметры", КоннекторHTTP.НовыеПараметры()); // BSLLS:MissingCommonModuleMethod-off
	Контекст.Вставить("Сессия", КоннекторHTTP.СоздатьСессию()); // BSLLS:MissingCommonModuleMethod-off
	
	Возврат Контекст;
	
КонецФункции

// Подготовить адрес сервера
//
// Параметры:
//  АдресСервера - Строка
//
// Возвращаемое значение:
//  Строка
//
Функция ПодготовитьАдресСервера(АдресСервера)
	
	НовыйАдресСервера = НРег(АдресСервера);
	НовыйАдресСервера = СтрЗаменить(НовыйАдресСервера, "https://", "");
	НовыйАдресСервера = СтрЗаменить(НовыйАдресСервера, "http://", "");
	
	Если СтрЗаканчиваетсяНа(НовыйАдресСервера, "/") Тогда
		НовыйАдресСервера = Лев(НовыйАдресСервера, СтрДлина(НовыйАдресСервера) - 1);
	КонецЕсли;
	
	Возврат НовыйАдресСервера;
	
КонецФункции

// Подготовить адрес ресурса
//
// Параметры:
//  АдресРесурса - Строка
//
// Возвращаемое значение:
//  Строка
//
Функция ПодготовитьАдресРесурса(АдресРесурса)
	
	НовыйАдресРесурса = НРег(АдресРесурса);
	
	Если СтрНачинаетсяС(НовыйАдресРесурса, "/") Тогда
		НовыйАдресРесурса = Прав(НовыйАдресРесурса, СтрДлина(НовыйАдресРесурса) - 1);
	КонецЕсли;
	
	Если СтрЗаканчиваетсяНа(НовыйАдресРесурса, "/") Тогда
		НовыйАдресРесурса = Лев(НовыйАдресРесурса, СтрДлина(НовыйАдресРесурса) - 1);
	КонецЕсли;
	
	Возврат НовыйАдресРесурса;
	
КонецФункции

// Сформировать URL
//
// Возвращаемое значение:
//  Строка
//
Функция СформироватьУРЛ()
	
	Фрагменты = Новый Массив;
	
	Если _Контекст.ЗащищенноеСоединение Тогда
		Фрагменты.Добавить("https://");
	Иначе
		Фрагменты.Добавить("http://");
	КонецЕсли;
	
	Фрагменты.Добавить(_Контекст.Сервер);
	
	Если ЗначениеЗаполнено(_Контекст.Порт) Тогда
		Фрагменты.Добавить(":");
		Фрагменты.Добавить(XMLСтрока(_Контекст.Порт));
	КонецЕсли;
	
	Фрагменты.Добавить("/");
	Фрагменты.Добавить(_Контекст.АдресРесурса);
	
	Возврат СтрСоединить(Фрагменты, "");
	
КонецФункции

#КонецОбласти
